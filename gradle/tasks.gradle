apply from: 'gradle/functions.gradle'
apply from: 'gradle/properties.gradle'

tasks.configure() {
	register(     'cleanBuild'     ) { group 'build'; dependsOn 'clean'; finalizedBy 'build' }

	register('cleanBuildDependants') { group 'build'; dependsOn 'clean'; finalizedBy 'buildDependants' }

	register(  'cleanBuildNeeded'  ) { group 'build'; dependsOn 'clean'; finalizedBy 'buildNeeded' }

	named('modrinth') {
		onlyIf {
			token = System.env.MODRINTH_TOKEN
		}

		versionType = System.env.RELEASE_TYPE ?: 'alpha'

		projectId = '9gx5Xvc5'
		versionNumber = "${project.mod_version}-${project.versionBuildName}${project.versionLabel}"
		versionName = "${toTitleCase versionType.toString()} ${project.mod_version} " +
 				(project.localBuild ? "Local Build ${project.localBuildNumber}" :
					"Build ${System.env.GITHUB_RUN_NUMBER}") + ' for Minecraft 1.18.x';
		changelog = isAlpha() ? '**USE WITH CAUTION**\n\nThis is an automatic alpha build.'
			: System.env.CHANGELOG ?: 'No changelog provided'

		uploadFile = remapJar

		addGameVersion '1.18'
		addGameVersion '1.18.1'
		addLoader 'fabric'
	}

	withType(JavaCompile).configureEach {
		// Minecraft 1.18 (1.18-pre2) upwards uses Java 17.
		it.options.release = 17

		doFirst {
			println "Compiling ${project.archivesBaseName}-${project.version}..."
		}
	}
}
