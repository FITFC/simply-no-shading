plugins {
	alias libs.plugins.fabric.loom
}

group = mod_group
archivesBaseName = "mc${libs.versions.minecraft.get()}-${mod_name}"
version = mod_version

repositories {
	maven { url 'https://maven.terraformersmc.com' }
}

dependencies {
	// Fabric Loader
	modImplementation libs.fabric.loader

	// Fabric Key Binding API (V1)
	modImplementation fabricApi.module('fabric-key-binding-api-v1', libs.versions.fabric.api.get())

	// Fabric Lifecycle Events (V1)
	modImplementation fabricApi.module('fabric-lifecycle-events-v1', libs.versions.fabric.api.get())

	// Fernflower
	runtimeOnly libs.fernflower

	// Minecraft
	minecraft libs.minecraft

	// ModMenu
	modImplementation libs.modmenu

	// Official Mojang Mappings
	mappings loom.officialMojangMappings()
}

processResources {
	inputs.property 'version', version

	filesMatching 'fabric.mod.json', {
		expand 'loader_version': libs.versions.fabric.loader.get().replace('+', 'x'),
		       'java_version': java.toolchain.languageVersion.get(),
		       'fabric_version': libs.versions.fabric.api.get(),
		       'minecraft_version': libs.versions.minecraft.get(),
		       'modmenu_version': libs.versions.modmenu.get().replace('+', 'x'),
		       'version': version
	}
}

java {
	toolchain.languageVersion = JavaLanguageVersion.of(17)

	withSourcesJar()
}

jar {
	from 'LICENSE', {
		rename { "${it}_${project.archivesBaseName}" }
	}
}

loom.runConfigs.configureEach {
	property 'mixin.debug.export', 'true'
}
